. as $root
| (
  [ 
    to_entries | sort_by(.value.name)[]
    | [
        "  <a class=\"panel-block\" data-target=\"#gherkin-dialect-", (.key|@html), "-content\">\n", (.value.name|@html), " (", (.key|@html), ")", "</a>\n",
        "  <div id=\"gherkin-dialect-", (.key|@html), "-content\" class=\"panel-block collapsed\">\n",
        "   <table class=\"table is-size-6\">\n",
        "     <thead>\n",
        "      <tr>\n",
        "       <th>English Keyword</th>\n",
        "       <th>", (.value.name|@html), " equivalent(s)</th>\n",
        "      </tr>\n",
        "     </thead>\n",
        "     <tbody>\n"
      ] + (
          [ .value
            | {"feature","background","scenario","scenarioOutline","examples","given","when","then","and","but"}
            | to_entries[]
            | "       <tr>\n",
              "         <td><code>", ([ .key ] | add), "</code></th>\n",
              "         <td>", ([ .value[] | "<code>", @html, "</code><br>"] | add), "</td>\n",
              "       </tr>\n"
          ]
      ) + [
        "     </tbody>\n",
        "   </table>\n",
        "  </div>\n"
      ]
    | add
  ]
  | add
  )
| "<div class=\"panel collapsible\">\n"	
+   .
+ "</div>\n"